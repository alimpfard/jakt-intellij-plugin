{
    parserClass="org.serenityos.jakt.parser.JaktParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Jakt"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.sdk.language.psi"
    psiImplPackage="org.intellij.sdk.language.psi.impl"

    elementTypeHolderClass="org.serenityos.jakt.JaktTypes"
    elementTypeClass="org.serenityos.jakt.plugin.JaktElement"
    tokenTypeClass="org.serenityos.jakt.plugin.JaktToken"

    parserUtilClass="org.serenityos.jakt.plugin.JaktParserUtil"

    tokens=[
        EXTERN_KEYWORD='extern'

        CLASS_KEYWORD='class'
        STRUCT_KEYWORD='struct'
        ENUM_KEYWORD='enum'
        FUNCTION_KEYWORD='function'
        PUBLIC_KEYWORD='public'
        PRIVATE_KEYWORD='private'

        LET_KEYWORD='let'

        MATCH_KEYWORD='match'
        IF_KEYWORD='if'
        ELSE_KEYWORD='else'

        WHILE_KEYWORD='while'
        LOOP_KEYWORD='loop'
        FOR_KEYWORD='for'
        IN_KEYWORD='in'
        UNSAFE='unsafe'
        CPP_KEYWORD='cpp'

        RETURN_KEYWORD='return'
        THROW_KEYWORD='throw'
        DEFER_KEYWORD='defer'

        TRUE_KEYWORD='true'
        FALSE_KEYWORD='false'

        MUTABLE_KEYWORD='mutable'
        ANONYMOUS_KEYWORD='anonymous'
        RAW_KEYWORD='raw'
        WEAK_KEYWORD='weak'
        THROWS_KEYWORD='throws'

        SOME_TYPE='Some'
        NONE_TYPE='None'

        PAREN_OPEN='('
        PAREN_CLOSE=')'
        CURLY_OPEN='{'
        CURLY_CLOSE='}'
        BRACKET_OPEN='['
        BRACKET_CLOSE=']'
        COLON=':'
        COLON_COLON='::'
        SEMICOLON=';'
        DOT='.'
        DOT_DOT='..'
        COMMA=','
        EQUALS='='
        DOUBLE_EQUALS='=='
        NOT_EQUALS='!='
        PLUS='+'
        MINUS='-'
        ASTERISK='*'
        SLASH='/'
        LEFT_SHIFT='<<'
        RIGHT_SHIFT='>>'
        ARITH_LEFT_SHIFT='<<<'
        ARITH_RIGHT_SHIFT='>>>'
        PERCENT='%'
        ARROW='->'
        FAT_ARROW='=>'
        QUESTION_MARK='?'
        DOUBLE_QUESTION_MARK='??'
        EXCLAMATION_POINT='!'
        LESS_THAN='<'
        LESS_THAN_EQUALS='<='
        GREATER_THAN='>'
        GREATER_THAN_EQUALS='>='
        AMPERSAND='&'
        PIPE='|'
        CARET='^'
        AND='and'
        OR='or'

        HEX_LITERAL="regexp:(0x|0X)[\dA-Fa-f](_?[\dA-Fa-f])*"
        BINARY_LITERAL="regexp:(0b|0B)[01](_?[01])*"
        DECIMAL_LITERAL="regexp:\d(_?\d)*"

        STRING_LITERAL="regexp:\"([^\"\\]|\\.)*\""
        CHAR_LITERAL="regexp:'([^'\\]|\\.)*'"
        IDENTIFIER="regexp:[A-Za-z_]\w*"

        SPACE="regexp:[ \t\n\x0B\f\r]+"
        COMMENT="regexp://[^\\R]*"
    ]

    extends(".+BinaryExpression")=BinaryExpression
    extends(".+Expression")=Expression
    extends(".+Statement")=Statement

    name(".+Expression")=expression
    name(".+Statement")=statement

    generateTokenAccessors=true
}

File ::= TopLevelDefinition*

private TopLevelDefinition ::=
      FunctionDeclaration
    | ClassDeclaration
    | StructDeclaration
    | EnumDeclaration
    {recoverWhile=TopLevelDefinition_recover}

private TopLevelDefinition_recover ::= !(EXTERN_KEYWORD | FUNCTION_KEYWORD | CLASS_KEYWORD | STRUCT_KEYWORD)

Qualifier ::=
      MemberQualifier
    | NamespacedMemberQualifier
    | ComputedMemberQualifier
    | PlainMemberQualifier
    {extends=Expression}

Statement ::=
      Expression
    | ReturnStatement
    | ThrowStatement
    | DeferStatement
    | IfStatement
    | WhileStatement
    | LoopStatement
    | ForStatement
    | VariableDeclarationStatement
    | UnsafeStatement
    | CppStatement

private StatementLead ::=
      EXTERN_KEYWORD
    | CLASS_KEYWORD
    | STRUCT_KEYWORD
    | FUNCTION_KEYWORD
    | LET_KEYWORD
    | WHILE_KEYWORD
    | FOR_KEYWORD
    | LOOP_KEYWORD
    | RETURN_KEYWORD
    | THROW_KEYWORD
    | DEFER_KEYWORD
    | THROWS_KEYWORD
    | SEMICOLON
    | ExpressionLead

private ExpressionLead ::=
      IF_KEYWORD
    | TRUE_KEYWORD
    | FALSE_KEYWORD
    | PAREN_OPEN
    | PAREN_CLOSE
    | CURLY_OPEN
    | CURLY_CLOSE
    | BRACKET_OPEN
    | BRACKET_CLOSE
    | PLUS
    | MINUS
    | EXCLAMATION_POINT
    | STRING_LITERAL
    | CHAR_LITERAL
    | Literal

/*************************************
 *               TYPES               *
 *************************************/

Type ::=
      RawType
    | WeakType
    | ArrayType
    | GenericType
    | DictionaryType
    | SetType
    | PlainType

RawType ::= RAW_KEYWORD Type
WeakType ::= WEAK_KEYWORD Type
PlainType ::= IDENTIFIER [ QUESTION_MARK | EXCLAMATION_POINT ]
GenericType ::= IDENTIFIER GenericBounds
GenericBounds ::= LESS_THAN <<list Type>> GREATER_THAN
ArrayType ::= BRACKET_OPEN IDENTIFIER BRACKET_CLOSE
DictionaryType ::= BRACKET_OPEN Type COLON Type BRACKET_CLOSE
SetType ::= CURLY_OPEN Type CURLY_CLOSE
TypeAnnotation ::= COLON Type

/*************************************
 *           Declarations            *
 *************************************/

FunctionDeclaration ::= FUNCTION_KEYWORD IDENTIFIER PAREN_OPEN ParameterList? PAREN_CLOSE FunctionReturnType? FunctionBody {pin=1}
FunctionReturnType ::= THROWS_KEYWORD? ARROW Type
private FunctionBody ::= Block | FAT_ARROW ExpressionWithoutAssignment
ParameterList ::= <<list Parameter>>
Parameter ::= ANONYMOUS_KEYWORD? MUTABLE_KEYWORD? IDENTIFIER TypeAnnotation?

ClassDeclaration ::= EXTERN_KEYWORD? CLASS_KEYWORD IDENTIFIER GenericBounds? StructBody
StructDeclaration ::= EXTERN_KEYWORD? STRUCT_KEYWORD IDENTIFIER GenericBounds? StructBody
StructBody ::= CURLY_OPEN StructMembers? CURLY_CLOSE

private StructMembers ::= StructMember (MemberSeparator StructMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
StructMember ::= StructVisibility? (StructField | FunctionDeclaration)
private StructMember_recover ::= !CURLY_CLOSE | (&MemberSeparator !IDENTIFIER)

StructVisibility ::= PUBLIC_KEYWORD | PRIVATE_KEYWORD
StructField ::= IDENTIFIER MUTABLE_KEYWORD? TypeAnnotation {pin=1}
MemberSeparator ::= COMMA | EOL {name="comma or eol"}

private EnumDeclaration ::= UnderlyingTypeEnumDeclaration | NormalEnumDeclaration

UnderlyingTypeEnumDeclaration ::= ENUM_KEYWORD IDENTIFIER TypeAnnotation OPEN_CURLY UnderlyingTypeEnumMembers CLOSE_CURLY

private UnderlyingTypeEnumMembers ::= UnderlyingTypeEnumMember (MemberSeparator UnderlyingTypeEnumMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
UnderlyingTypeEnumMember ::= IDENTIFIER (EQUALS Expression)?

NormalEnumDeclaration ::= ENUM_KEYWORD IDENTIFIER GenericBounds? OPEN_CURLY NormalEnumMembers CLOSE_CURLY

private NormalEnumMembers ::= NormalEnumMember (MemberSeparator NormalEnumMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
private NormalEnumMember ::= TypedNormalEnumMember | StructNormalEnumMember
TypedNormalEnumMember ::= IDENTIFIER (OPEN_PAREN Type CLOSE_PAREN)?

StructNormalEnumMember ::= IDENTIFIER OPEN_PAREN StructNormalEnumMemberBody? CLOSE_PAREN
private StructNormalEnumMemberBody ::= StructNormalEnumMemberPart (MemberSeparator StructNormalEnumMemberPart)* {
    pin=1
    recoverWhile=StructMember_recover
}
private StructNormalEnumMemberPart ::= IDENTIFIER TypeAnnotation

/*************************************
 *            STATEMENTS             *
 *************************************/

VariableDeclarationStatement ::= LET_KEYWORD MUTABLE_KEYWORD? IDENTIFIER TypeAnnotation? EQUALS ExpressionWithoutAssignment {pin=1}
IfStatement ::= IF_KEYWORD ExpressionWithoutAssignment Block [ ELSE_KEYWORD Block ]
ReturnStatement ::= RETURN_KEYWORD ExpressionWithoutAssignment?
ThrowStatement ::= THROW_KEYWORD ExpressionWithoutAssignment
DeferStatement ::= DEFER_KEYWORD Statement
WhileStatement ::= WHILE_KEYWORD ExpressionWithoutAssignment Block
LoopStatement ::= LOOP_KEYWORD Block

ForStatement ::= FOR_KEYWORD IDENTIFIER IN_KEYWORD ExpressionWithoutAssignment Block

LabeledArgument ::= IDENTIFIER COLON ExpressionWithoutAssignment
UnlabeledArgument ::= ExpressionWithoutAssignment

// TODO: Maybe delegate unsafe checking to plugin analysis for better error messages?
UnsafeStatement ::= UNSAFE_KEYWORD <<withOn Block "UNSAFE">>
CppStatement ::= <<isModeOn "UNSAFE">> CPP_KEYWORD Block

/*************************************
 *            EXPRESSIONS            *
 *************************************/

Expression ::=
      LogicalOrBinaryExpression
    | LogicalAndBinaryExpression
    | BitwiseOrBinaryExpression
    | BitwiseXorBinaryExpression
    | BitwiseAndBinaryExpression
    | RelationalBinaryExpression
    | ShiftBinaryExpression
    | AddBinaryExpression
    | MultiplyBinaryExpression
    | AssignmentBinaryExpression
    | ParenExpression
    | OptionalSomeExpression // Must come before CallExpression
    | CallExpression
    | RangeExpression
    | ArrayExpression
    | DictionaryExpression
    | SetExpression
    | MatchExpression
    | Literal
    | OptionalNoneExpression // Must come before Qualifier
    | Qualifier
    | Block

private ExpressionWithoutAssignment ::= <<withOn Expression "NO_ASSIGN">>

MemberQualifier ::= IDENTIFIER DOT IDENTIFIER {methods=[left="IDENTIFIER[0]" right="IDENTIFIER[1]"]}
NamespacedMemberQualifier ::= IDENTIFIER (COLON_COLON IDENTIFIER)+
ComputedMemberQualifier ::= IDENTIFIER BRACKET_OPEN Expression BRACKET_CLOSE
PlainMemberQualifier ::= IDENTIFIER

CallExpression ::= Qualifier PAREN_OPEN ArgumentList? PAREN_CLOSE {pin=2 recoverWhile=CallExpression_recover}
ArgumentList ::= <<list Argument>>
Argument ::= LabeledArgument | UnlabeledArgument
private CallExpression_recover ::= !<<eof>>

RangeExpression ::= NumericLiteral DOT_DOT NumericLiteral
ParenExpression ::= PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE

OptionalSomeExpression ::= SOME_TYPE PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE
OptionalNoneExpression ::= NONE_TYPE

fake BinaryExpression ::= Expression+ {
    methods=[left="Expression[0]" right="Expression[1]"]
}

LogicalOrBinaryExpression ::= Expression LogicalOrOp Expression
LogicalAndBinaryExpression ::= Expression AND Expression
BitwiseOrBinaryExpression ::= Expression PIPE Expression
BitwiseXorBinaryExpression ::= Expression CARET Expression
BitwiseAndBinaryExpression ::= Expression AMPERSAND Expression
RelationalBinaryExpression ::= Expression RelationalOp Expression
ShiftBinaryExpression ::= Expression ShiftOp Expression
AddBinaryExpression ::= Expression AddOp Expression
MultiplyBinaryExpression ::= Expression MultiplyOp Expression
AssignmentBinaryExpression ::= <<isModeOff "NO_ASSIGN">> Qualifier AssignmentOp Expression {rightAssociative=true}

private AddOp ::= PLUS | MINUS
private MultiplyOp ::= ASTERISK | SLASH | PERCENT
private ShiftOp ::= ARITH_LEFT_SHIFT | LEFT_SHIFT | ARITH_RIGHT_SHIFT | RIGHT_SHIFT
private RelationalOp ::= LESS_THAN_EQUALS | LESS_THAN | GREATER_THAN_EQUALS | GREATER_THAN | DOUBLE_EQUALS | NOT_EQUALS
private LogicalOrOp ::= OR | DOUBLE_QUESTION_MARK
private AssignmentOp ::= (AddOp | MultiplyOp | ShiftOp)? EQUALS

Block ::= CURLY_OPEN <<withOff BlockElement* "NO_ASSIGN">> CURLY_CLOSE {pin(".*")=1 extends=Expression}
private BlockElement ::= !CURLY_CLOSE Statement {recoverWhile=BlockElement_recover}
// TODO: Include newlines?
private BlockElement_recover ::= !(CURLY_CLOSE | ';' | <<eof>> | ExpressionLead | StatementLead)

ArrayExpression ::= BRACKET_OPEN (SizedArrayBody | ElementsArrayBody)? BRACKET_CLOSE
SizedArrayBody ::= ExpressionWithoutAssignment SEMICOLON NumericLiteral
ElementsArrayBody ::= <<list ExpressionWithoutAssignment>>

DictionaryExpression ::= BRACKET_OPEN <<list DictionaryElement>>? BRACKET_CLOSE
DictionaryElement ::= Expression COLON Expression

SetExpression ::= CURLY_OPEN <<list ExpressionWithoutAssignment>>? CURLY_CLOSE

MatchExpression ::= MATCH_KEYWORD Expression OPEN_CURLY MatchPatterns CLOSE_CURLY
private MatchPatterns ::= MatchPattern (MemberSeparator MatchPattern)* {
    pin=1
    recoverWhile=MatchPatterns_recover
}
private MatchPatterns_recover ::= !CURLY_CLOSE | (&MemberSeparator (ELSE_KEYWORD | OPEN_PAREN | IDENTIFIER))
MatchPattern ::= PatternHead PatternTrail
PatternTrail ::= FAT_ARROW Expression
PatternHead ::= ELSE_KEYWORD | IdentPatternHead | ExpressionPatternHead
// TODO: This is incomplete
IdentPatternHead ::= (IDENTIFIER | NamespacedMemberQualifier) OPEN_PAREN <<list IDENTIFIER>> CLOSE_PAREN
ExpressionPatternHead ::= OPEN_PAREN ExpressionWithoutAssignment CLOSE_PAREN

Literal ::=
      STRING_LITERAL
    | CHAR_LITERAL
    | NumericLiteral
    | BooleanLiteral
    {extends=Expression}

BooleanLiteral ::= TRUE_KEYWORD | FALSE_KEYWORD

NumericLiteral ::=
      BINARY_LITERAL
    | HEX_LITERAL
    | DECIMAL_LITERAL

// Helpers
private meta list ::= <<p>> (COMMA <<p>>)*
