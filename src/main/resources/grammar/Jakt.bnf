{
    parserClass="org.serenityos.jakt.parser.JaktParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Jakt"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.sdk.language.psi"
    psiImplPackage="org.intellij.sdk.language.psi.impl"

    elementTypeHolderClass="org.serenityos.jakt.JaktTypes"
    elementTypeClass="org.serenityos.jakt.plugin.JaktElement"
    tokenTypeClass="org.serenityos.jakt.plugin.JaktToken"

    parserUtilClass="org.serenityos.jakt.plugin.JaktParserUtil"

    tokens=[
        EXTERN_KEYWORD='extern'

        CLASS_KEYWORD='class'
        STRUCT_KEYWORD='struct'
        ENUM_KEYWORD='enum'
        FUNCTION_KEYWORD='function'
        PUBLIC_KEYWORD='public'
        PRIVATE_KEYWORD='private'

        LET_KEYWORD='let'
        MATCH_KEYWORD='match'
        IF_KEYWORD='if'
        ELSE_KEYWORD='else'

        WHILE_KEYWORD='while'
        LOOP_KEYWORD='loop'
        FOR_KEYWORD='for'
        IN_KEYWORD='in'
        UNSAFE_KEYWORD='unsafe'
        CPP_KEYWORD='cpp'

        RETURN_KEYWORD='return'
        THROW_KEYWORD='throw'
        DEFER_KEYWORD='defer'

        TRUE_KEYWORD='true'
        FALSE_KEYWORD='false'

        REF_KEYWORD='ref'
        MUTABLE_KEYWORD='mutable'
        ANONYMOUS_KEYWORD='anonymous'
        RAW_KEYWORD='raw'
        WEAK_KEYWORD='weak'
        THROWS_KEYWORD='throws'

        SOME_TYPE='Some'
        NONE_TYPE='None'

        PAREN_OPEN='('
        PAREN_CLOSE=')'
        CURLY_OPEN='{'
        CURLY_CLOSE='}'
        BRACKET_OPEN='['
        BRACKET_CLOSE=']'
        COLON=':'
        COLON_COLON='::'
        SEMICOLON=';'
        DOT='.'
        DOT_DOT='..'
        COMMA=','
        EQUALS='='
        DOUBLE_EQUALS='=='
        NOT_EQUALS='!='
        PLUS='+'
        MINUS='-'
        ASTERISK='*'
        SLASH='/'
        PLUS_PLUS='++'
        MINUS_MINUS='--'
        LEFT_SHIFT='<<'
        RIGHT_SHIFT='>>'
        ARITH_LEFT_SHIFT='<<<'
        ARITH_RIGHT_SHIFT='>>>'
        PERCENT='%'
        ARROW='->'
        FAT_ARROW='=>'
        QUESTION_MARK='?'
        DOUBLE_QUESTION_MARK='??'
        EXCLAMATION_POINT='!'
        LESS_THAN='<'
        LESS_THAN_EQUALS='<='
        GREATER_THAN='>'
        GREATER_THAN_EQUALS='>='
        AMPERSAND='&'
        PIPE='|'
        CARET='^'
        TILDE='~'
        AND='and'
        OR='or'
        NOT='not'

        HEX_LITERAL="regexp:(0x|0X)[\dA-Fa-f](_?[\dA-Fa-f])*"
        BINARY_LITERAL="regexp:(0b|0B)[01](_?[01])*"
        DECIMAL_LITERAL="regexp:\d(_?\d)*"

        STRING_LITERAL="regexp:\"([^\"\\]|\\.)*\""
        BYTE_CHAR_LITERAL="regexp:b'([^'\\]|\\.)*'"
        CHAR_LITERAL="regexp:'([^'\\]|\\.)*'"
        IDENTIFIER="regexp:[A-Za-z_]\w*"

        COMMENT="regexp://[^\r\n]*"
    ]

    extends(".+BinaryExpression")=BinaryExpression
    extends(".+Expression")=Expression
    extends(".+Statement")=Statement

    name(".+Expression")=expression
    name(".+Statement")=statement

    generateTokenAccessors=true
}

File ::= TopLevelDefinition*

private TopLevelDefinition ::=
      FunctionDeclaration
    | ClassDeclaration
    | StructDeclaration
    | EnumDeclaration
    {recoverWhile=TopLevelDefinition_recover}

private TopLevelDefinition_recover ::= !(
      EXTERN_KEYWORD
    | FUNCTION_KEYWORD
    | CLASS_KEYWORD
    | STRUCT_KEYWORD
    | ENUM_KEYWORD
    | REF_KEYWORD)

Qualifier ::=
      MemberQualifier
    | NamespacedMemberQualifier
    | ComputedMemberQualifier
    | PlainMemberQualifier
    {extends=Expression}

Statement ::=
      Expression
    | ReturnStatement
    | ThrowStatement
    | DeferStatement
    | IfStatement
    | WhileStatement
    | LoopStatement
    | ForStatement
    | VariableDeclarationStatement
    | UnsafeStatement
    | CppStatement

private StatementLead ::=
      EXTERN_KEYWORD
    | CLASS_KEYWORD
    | STRUCT_KEYWORD
    | FUNCTION_KEYWORD
    | LET_KEYWORD
    | WHILE_KEYWORD
    | FOR_KEYWORD
    | LOOP_KEYWORD
    | RETURN_KEYWORD
    | THROW_KEYWORD
    | DEFER_KEYWORD
    | THROWS_KEYWORD
    | SEMICOLON
    | ExpressionLead

private ExpressionLead ::=
      IF_KEYWORD
    | TRUE_KEYWORD
    | FALSE_KEYWORD
    | PAREN_OPEN
    | PAREN_CLOSE
    | CURLY_OPEN
    | CURLY_CLOSE
    | BRACKET_OPEN
    | BRACKET_CLOSE
    | PLUS
    | MINUS
    | EXCLAMATION_POINT
    | STRING_LITERAL
    | CHAR_LITERAL
    | Literal

/*************************************
 *               TYPES               *
 *************************************/

Type ::=
      RawType
    | WeakType
    | ArrayType
    | GenericType
    | DictionaryType
    | SetType
    | TupleType
    | PlainType

RawType ::= RAW_KEYWORD Type
WeakType ::= WEAK_KEYWORD Type
PlainType ::= IDENTIFIER [ QUESTION_MARK | EXCLAMATION_POINT ]
GenericType ::= IDENTIFIER GenericBounds
GenericBounds ::= LESS_THAN <<list Type>> GREATER_THAN
ArrayType ::= BRACKET_OPEN IDENTIFIER BRACKET_CLOSE
DictionaryType ::= BRACKET_OPEN Type COLON Type BRACKET_CLOSE
SetType ::= CURLY_OPEN Type CURLY_CLOSE
TupleType ::= PAREN_OPEN <<list Type>> PAREN_CLOSE
TypeAnnotation ::= COLON Type

/*************************************
 *           Declarations            *
 *************************************/

FunctionDeclaration ::= FUNCTION_KEYWORD IDENTIFIER GenericBounds? PAREN_OPEN ParameterList? PAREN_CLOSE FunctionReturnType? FunctionBody {pin=1}
FunctionReturnType ::= THROWS_KEYWORD? ARROW Type
private FunctionBody ::= Block | FAT_ARROW ExpressionWithoutAssignment
ParameterList ::= <<list Parameter>>
Parameter ::= ANONYMOUS_KEYWORD? MUTABLE_KEYWORD? IDENTIFIER TypeAnnotation?

ClassDeclaration ::= EXTERN_KEYWORD? CLASS_KEYWORD IDENTIFIER GenericBounds? StructBody
StructDeclaration ::= EXTERN_KEYWORD? STRUCT_KEYWORD IDENTIFIER GenericBounds? StructBody
StructBody ::= CURLY_OPEN StructMembers? CURLY_CLOSE

private StructMembers ::= StructMember (MemberSeparator StructMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
StructMember ::= StructVisibility? (StructField | FunctionDeclaration)
private StructMember_recover ::= !CURLY_CLOSE | (&MemberSeparator !IDENTIFIER)

StructVisibility ::= PUBLIC_KEYWORD | PRIVATE_KEYWORD
StructField ::= IDENTIFIER MUTABLE_KEYWORD? TypeAnnotation {pin=1}
// TODO: Handle EOL manually and make this comma or EOL
MemberSeparator ::= COMMA {name="comma"}

EnumDeclaration ::= EnumHead EnumBody
private EnumBody ::= UnderlyingTypeEnumBody | NormalEnumBody
private EnumHead ::= REF_KEYWORD? ENUM_KEYWORD IDENTIFIER

UnderlyingTypeEnumBody ::= TypeAnnotation CURLY_OPEN UnderlyingTypeEnumMembers CURLY_CLOSE {pin=1}

private UnderlyingTypeEnumMembers ::= UnderlyingTypeEnumMember (MemberSeparator UnderlyingTypeEnumMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
UnderlyingTypeEnumMember ::= IDENTIFIER [EQUALS Expression]

NormalEnumBody ::= GenericBounds? CURLY_OPEN NormalEnumMembers CURLY_CLOSE

private NormalEnumMembers ::= NormalEnumMember (MemberSeparator NormalEnumMember)* {
    pin=1
    recoverWhile=StructMember_recover
}
private NormalEnumMember ::= IDENTIFIER NormalEnumMemberBody?
private NormalEnumMemberBody ::= PAREN_OPEN (StructEnumMemberBody | TypeEnumMemberBody) PAREN_CLOSE
TypeEnumMemberBody ::= <<list Type>>
StructEnumMemberBody ::= <<list (IDENTIFIER TypeAnnotation)>>

/*************************************
 *            STATEMENTS             *
 *************************************/

VariableDeclarationStatement ::= LET_KEYWORD MUTABLE_KEYWORD? IDENTIFIER TypeAnnotation? EQUALS ExpressionWithoutAssignment {pin=1}
IfStatement ::= IF_KEYWORD ExpressionWithoutAssignment Block [ ELSE_KEYWORD Block ]
ReturnStatement ::= RETURN_KEYWORD ExpressionWithoutAssignment?
ThrowStatement ::= THROW_KEYWORD ExpressionWithoutAssignment
DeferStatement ::= DEFER_KEYWORD Statement
WhileStatement ::= WHILE_KEYWORD ExpressionWithoutAssignment Block
LoopStatement ::= LOOP_KEYWORD Block

ForStatement ::= FOR_KEYWORD IDENTIFIER IN_KEYWORD ExpressionWithoutAssignment Block

// TODO: Maybe delegate unsafe checking to plugin analysis for better error messages?
UnsafeStatement ::= UNSAFE_KEYWORD <<withOn Block "UNSAFE">>
CppStatement ::= <<isModeOn "UNSAFE">> CPP_KEYWORD Block

/*************************************
 *            EXPRESSIONS            *
 *************************************/

Expression ::=
      LogicalOrBinaryExpression
    | LogicalAndBinaryExpression
    | BitwiseOrBinaryExpression
    | BitwiseXorBinaryExpression
    | BitwiseAndBinaryExpression
    | RelationalBinaryExpression
    | ShiftBinaryExpression
    | AddBinaryExpression
    | MultiplyBinaryExpression
    | PrefixUnaryExpression
    | PostfixUnaryExpression
    | AssignmentBinaryExpression
    | ParenExpression
    | OptionalSomeExpression // Must come before CallExpression
    | CallExpression
    | RangeExpression
    | ArrayExpression
    | DictionaryExpression
    | SetExpression
    | TupleExpression
    | MatchExpression
    | Literal
    | OptionalNoneExpression // Must come before Qualifier
    | Qualifier
    | Block

private ExpressionWithoutAssignment ::= <<withOn Expression "NO_ASSIGN">>

MemberQualifier ::= IDENTIFIER DOT IDENTIFIER
NamespacedMemberQualifier ::= IDENTIFIER (COLON_COLON IDENTIFIER)+
ComputedMemberQualifier ::= IDENTIFIER BRACKET_OPEN Expression BRACKET_CLOSE
PlainMemberQualifier ::= IDENTIFIER

CallExpression ::= Qualifier PAREN_OPEN ArgumentList? PAREN_CLOSE {pin=2 recoverWhile=CallExpression_recover}
ArgumentList ::= <<list Argument>>
Argument ::= LabeledArgument | UnlabeledArgument
LabeledArgument ::= IDENTIFIER COLON ExpressionWithoutAssignment
UnlabeledArgument ::= ExpressionWithoutAssignment
private CallExpression_recover ::= !<<eof>>

RangeExpression ::= NumericLiteral DOT_DOT NumericLiteral
ParenExpression ::= PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE

OptionalSomeExpression ::= SOME_TYPE PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE
OptionalNoneExpression ::= NONE_TYPE

fake BinaryExpression ::= Expression+ {
    methods=[left="Expression[0]" right="Expression[1]"]
}

LogicalOrBinaryExpression ::= Expression LogicalOrOp Expression
LogicalAndBinaryExpression ::= Expression AND Expression
BitwiseOrBinaryExpression ::= Expression PIPE Expression
BitwiseXorBinaryExpression ::= Expression CARET Expression
BitwiseAndBinaryExpression ::= Expression AMPERSAND Expression
RelationalBinaryExpression ::= Expression RelationalOp Expression
ShiftBinaryExpression ::= Expression ShiftOp Expression
AddBinaryExpression ::= Expression AddOp Expression
MultiplyBinaryExpression ::= Expression MultiplyOp Expression
AssignmentBinaryExpression ::= <<isModeOff "NO_ASSIGN">> Qualifier AssignmentOp Expression {rightAssociative=true}
fake UnaryExpression ::= PrefixOp? Expression PostfixOp?
PrefixUnaryExpression ::= PrefixOp Expression {extends=UnaryExpression}
PostfixUnaryExpression ::= Expression PostfixOp {extends=UnaryExpression}

private AddOp ::= PLUS | MINUS
private MultiplyOp ::= ASTERISK | SLASH | PERCENT
private ShiftOp ::= ARITH_LEFT_SHIFT | LEFT_SHIFT | ARITH_RIGHT_SHIFT | RIGHT_SHIFT
private RelationalOp ::= LESS_THAN_EQUALS | LESS_THAN | GREATER_THAN_EQUALS | GREATER_THAN | DOUBLE_EQUALS | NOT_EQUALS
private LogicalOrOp ::= OR | DOUBLE_QUESTION_MARK
private AssignmentOp ::= (AddOp | MultiplyOp | ShiftOp)? EQUALS
private PrefixOp ::=
      PLUS_PLUS
    | MINUS_MINUS
    | MINUS
    | NOT
    | TILDE
    | AMPERSAND RAW_KEYWORD
    | <<isModeOn "UNSAFE">> ASTERISK
private PostfixOp ::= PLUS_PLUS | MINUS_MINUS |

Block ::= CURLY_OPEN <<withOff Statement* "NO_ASSIGN">> CURLY_CLOSE {
    pin=1
    recoverWhile=Block_recover
    extends=Expression
}
// TODO: Include newlines?
private Block_recover ::= !(CURLY_CLOSE | <<eof>>)

ArrayExpression ::= BRACKET_OPEN (SizedArrayBody | ElementsArrayBody)? BRACKET_CLOSE
SizedArrayBody ::= ExpressionWithoutAssignment SEMICOLON NumericLiteral
ElementsArrayBody ::= <<list ExpressionWithoutAssignment>>

DictionaryExpression ::= BRACKET_OPEN <<list DictionaryElement>>? BRACKET_CLOSE
DictionaryElement ::= Expression COLON Expression
SetExpression ::= CURLY_OPEN <<list ExpressionWithoutAssignment>>? CURLY_CLOSE
TupleExpression ::= PAREN_OPEN <<list ExpressionWithoutAssignment>> PAREN_CLOSE

MatchExpression ::= MATCH_KEYWORD ExpressionWithoutAssignment CURLY_OPEN MatchPatterns? CURLY_CLOSE
private MatchPatterns ::= MatchPattern (MemberSeparator MatchPattern)* {
    pin=1
    recoverWhile=MatchPatterns_recover
}
private MatchPatterns_recover ::= !CURLY_CLOSE | !(&MemberSeparator (ELSE_KEYWORD | PAREN_OPEN | IDENTIFIER))
MatchPattern ::= PatternHead PatternTrail
PatternTrail ::= FAT_ARROW Expression
PatternHead ::= ELSE_KEYWORD | IdentPatternHead | ExpressionPatternHead
// TODO: This is incomplete
IdentPatternHead ::= (IDENTIFIER | NamespacedMemberQualifier) PAREN_OPEN <<list IDENTIFIER>> PAREN_CLOSE
ExpressionPatternHead ::= PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE

Literal ::=
      STRING_LITERAL
    | CHAR_LITERAL
    | NumericLiteral
    | BooleanLiteral
    {extends=Expression}

BooleanLiteral ::= TRUE_KEYWORD | FALSE_KEYWORD

NumericLiteral ::=
      BINARY_LITERAL
    | HEX_LITERAL
    | DECIMAL_LITERAL

// Helpers
private meta list ::= <<p>> (COMMA <<p>>)*
