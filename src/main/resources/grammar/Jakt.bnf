{
    parserClass="org.serenityos.jakt.parser.JaktParser"

    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="Jakt"
    psiImplClassSuffix="Impl"
    psiPackage="org.intellij.sdk.language.psi"
    psiImplPackage="org.intellij.sdk.language.psi.impl"

    elementTypeHolderClass="org.serenityos.jakt.JaktTypes"
    elementTypeClass="org.serenityos.jakt.syntax.JaktElementType"
    tokenTypeClass="org.serenityos.jakt.syntax.JaktToken"

    parserUtilClass="org.serenityos.jakt.syntax.JaktParserUtil"

    tokens=[
        COMMENT=""
    ]

    implements(".*")="org.serenityos.jakt.psi.JaktPsiElement"

    extends(".+BinaryExpression")=BinaryExpression
    extends(".+Expression")=Expression
    extends(".+Statement")=Statement

    name(".+Expression")="expression"
    name(".+Statement")="statement"

    generateTokenAccessors=true
}

/*************************************
 *               ROOT                *
 *************************************/

File ::= NL TopLevelDefinitionList? NL

private TopLevelDefinitionList ::= TopLevelDefinition (StatementSeparator TopLevelDefinition)*

TopLevelDefinition ::=
      InternalFunctionDeclaration
    | ExternalFunctionDeclaration
    | InternalStructDeclaration
    | ExternalStructDeclaration
    | EnumDeclaration
    | NamespaceDeclaration
    | ImportStatement {
    implements="org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    // For some reason this gets a getExternKeyword() function which conflicts
    // with ExternStructDeclaration
    generateTokenAccessors=false
}

//private StatementLead ::=
//      EXTERN_KEYWORD
//    | CLASS_KEYWORD
//    | STRUCT_KEYWORD
//    | FUNCTION_KEYWORD
//    | LET_KEYWORD
//    | MUT_KEYWORD
//    | WHILE_KEYWORD
//    | FOR_KEYWORD
//    | LOOP_KEYWORD
//    | RETURN_KEYWORD
//    | THROW_KEYWORD
//    | DEFER_KEYWORD
//    | THROWS_KEYWORD
//    | SEMICOLON
//    | ExpressionLead

private ExpressionLead ::=
      IF_KEYWORD
    | TRUE_KEYWORD
    | FALSE_KEYWORD
    | PAREN_OPEN
    | PAREN_CLOSE
    | CURLY_OPEN
    | CURLY_CLOSE
    | BRACKET_OPEN
    | BRACKET_CLOSE
    | PLUS
    | MINUS
    | EXCLAMATION_POINT
    | STRING_LITERAL
    | CHAR_LITERAL
    | Literal

NamespaceDeclaration ::= NAMESPACE_KEYWORD IDENTIFIER NamespaceBody {
    extends=TopLevelDefinition
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktNamespacedDeclarationMixin"
}

NamespaceBody ::= CURLY_OPEN NL TopLevelDefinitionList? NL CURLY_CLOSE

// Import Statement
ImportStatement ::= IMPORT_KEYWORD IDENTIFIER ImportAs? ImportBraceList? {
    extends=TopLevelDefinition
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktImportStatementMixin"
}
private ImportAs ::= KEYWORD_AS IDENTIFIER
ImportBraceList ::= CURLY_OPEN <<commaOrEolList ImportBraceEntry>>? CURLY_CLOSE
ImportBraceEntry ::= IDENTIFIER {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktImportBraceEntryMixin"
}

/***************************
 *       FUNCTIONS         *
 ***************************/

fake FunctionDeclaration ::= EXTERN_KEYWORD? FunctionHeader FunctionBody? {
    extends=TopLevelDefinition
    implements=[
        "org.serenityos.jakt.psi.declaration.JaktDeclaration"
        "org.serenityos.jakt.psi.api.JaktPsiScope"
        "org.serenityos.jakt.psi.caching.JaktModificationBoundary"
        "org.serenityos.jakt.psi.declaration.JaktGeneric"
    ]
    mixin="org.serenityos.jakt.psi.declaration.JaktFunctionDeclarationMixin"
}

private FunctionHeader ::= FUNCTION_KEYWORD IDENTIFIER GenericBounds? ParameterList FunctionReturnType
FunctionReturnType ::= THROWS_KEYWORD? [ARROW Type]
private FunctionBody ::= Block | FAT_ARROW NL ExpressionWithoutAssignment

InternalFunctionDeclaration ::= FunctionHeader FunctionBody {elementType=FunctionDeclaration}
ExternalFunctionDeclaration ::= EXTERN_KEYWORD FunctionHeader {elementType=FunctionDeclaration}

ParameterList ::= PAREN_OPEN [(ThisParameter | Parameter) (COMMA Parameter)*] PAREN_CLOSE
ThisParameter ::= MUT_KEYWORD? THIS_KEYWORD
Parameter ::= ANON_KEYWORD? MUT_KEYWORD? IDENTIFIER TypeAnnotation {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktParameterMixin"
}

/*************************
 *       STRUCTS         *
 *************************/

fake StructDeclaration ::= EXTERN_KEYWORD? StructHeader StructBody {
    extends=TopLevelDefinition
    implements=[
        "org.serenityos.jakt.psi.api.JaktPsiScope"
        "org.serenityos.jakt.psi.declaration.JaktDeclaration"
        "org.serenityos.jakt.psi.declaration.JaktGeneric"
    ]
    mixin="org.serenityos.jakt.psi.declaration.JaktStructDeclarationMixin"
}

private StructHeader ::= (CLASS_KEYWORD | STRUCT_KEYWORD) IDENTIFIER GenericBounds? NL

fake StructBody ::= CURLY_OPEN NL <<commaOrEolList StructMember>>? NL CURLY_CLOSE
fake StructMember ::= StructField | StructMethod

StructField ::= StructVisibility? IDENTIFIER TypeAnnotation {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktStructFieldMixin"
}

fake StructMethod ::= StructVisibility? FunctionDeclaration

StructVisibility ::= PUBLIC_KEYWORD | PRIVATE_KEYWORD |  RestrictedVisibility
private RestrictedVisibility ::= RESTRICTED_KEYWORD PAREN_OPEN <<commaOrEolList Type>>? PAREN_CLOSE
MemberSeparator ::= COMMA? NEWLINE+ | COMMA {name="comma or newline"}

// INTERNAL

InternalStructDeclaration ::= StructHeader InternalStructBody {elementType=StructDeclaration}
InternalStructBody ::= CURLY_OPEN NL <<commaOrEolList InternalStructMember>>? NL CURLY_CLOSE {elementType=StructBody}
InternalStructMember ::= StructField | InternalStructMethod {elementType=StructMember}
InternalStructMethod ::= StructVisibility? InternalFunctionDeclaration {elementType=StructMethod}

// EXTERNAL

ExternalStructDeclaration ::= EXTERN_KEYWORD StructHeader ExternalStructBody {elementType=StructDeclaration}
ExternalStructBody ::= CURLY_OPEN NL <<commaOrEolList ExternalStructMember>>? NL CURLY_CLOSE {elementType=StructBody}
ExternalStructMember ::= StructField | ExternalStructMethod {elementType=StructMember}
ExternalStructMethod ::= StructVisibility? ExternalStructFunctionDeclaration {elementType=StructMethod}
// Has to be different due to not requiring the extern keyword
ExternalStructFunctionDeclaration ::= FunctionHeader {elementType=FunctionDeclaration}

/*******************
 *      Enums      *
 *******************/

EnumDeclaration ::= EnumHead EnumBody {
    extends=TopLevelDefinition
    implements=[
        "org.serenityos.jakt.psi.api.JaktPsiScope"
        "org.serenityos.jakt.psi.declaration.JaktDeclaration"
        "org.serenityos.jakt.psi.declaration.JaktGeneric"
    ]
    mixin="org.serenityos.jakt.psi.declaration.JaktEnumDeclarationMixin"
}
private EnumHead ::= BOXED_KEYWORD? ENUM_KEYWORD NL IDENTIFIER NL
private EnumBody ::= UnderlyingTypeEnumBody | NormalEnumBody

UnderlyingTypeEnumBody ::= TypeAnnotation NL CURLY_OPEN NL UnderlyingTypeEnumMembers CURLY_CLOSE

fake EnumVariant ::= IDENTIFIER [(EQUALS Expression) | NormalEnumMemberBody] {
    name="enum variant"
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktEnumVariantMixin"
}

private UnderlyingTypeEnumMembers ::= <<commaOrEolList UnderlyingTypeEnumVariant>>?
UnderlyingTypeEnumVariant ::= IDENTIFIER [EQUALS Expression] {elementType=EnumVariant}

NormalEnumBody ::= GenericBounds? NL CURLY_OPEN NL NormalEnumMembers CURLY_CLOSE

private NormalEnumMembers ::= <<commaOrEolList NormalEnumMember>>?
NormalEnumMember ::= NormalEnumVariant | (StructVisibility? InternalFunctionDeclaration)
NormalEnumVariant ::= IDENTIFIER NormalEnumMemberBody? {elementType=EnumVariant}
NormalEnumMemberBody ::= PAREN_OPEN NL [TypeEnumMemberBody | StructEnumMemberBody] PAREN_CLOSE
TypeEnumMemberBody ::= <<commaList (Type !COLON)>>
private StructEnumMemberBody ::= <<commaOrEolList StructEnumMemberBodyPart>>
StructEnumMemberBodyPart ::= StructEnumMemberLabel TypeAnnotation
StructEnumMemberLabel ::= IDENTIFIER {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktStructEnumMemberLabelMixin"
}

/*************************************
 *            STATEMENTS             *
 *************************************/

private StatementList ::= Statement (StatementSeparator Statement)*
private StatementSeparator ::= (NEWLINE | SEMICOLON)+

Statement ::=
      Expression
    | ReturnStatement
    | ThrowStatement
    | DeferStatement
    | IfStatement
    | WhileStatement
    | LoopStatement
    | ForStatement
    | VariableDeclarationStatement
    | TryStatement
    | YieldStatement
    | BreakStatement
    | ContinueStatement
    | UnsafeStatement
    | InlineCppStatement
    | Block

VariableDeclarationStatement ::= (LET_KEYWORD | MUT_KEYWORD) IDENTIFIER TypeAnnotation? EQUALS ExpressionWithoutAssignment {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktVariableDeclarationMixin"
}
IfStatement ::= IF_KEYWORD ExpressionWithoutAssignment Block [ELSE_KEYWORD (IfStatement | Block)]
ReturnStatement ::= RETURN_KEYWORD ExpressionWithoutAssignment?
ThrowStatement ::= THROW_KEYWORD ExpressionWithoutAssignment
DeferStatement ::= DEFER_KEYWORD Statement
WhileStatement ::= WHILE_KEYWORD ExpressionWithoutAssignment Block
LoopStatement ::= LOOP_KEYWORD Block
TryStatement ::= TRY_KEYWORD Statement CATCH_KEYWORD IDENTIFIER Statement
BreakStatement ::= BREAK_KEYWORD
ContinueStatement ::= CONTINUE_KEYWORD
YieldStatement ::= YIELD_KEYWORD ExpressionWithoutAssignment

ForStatement ::= FOR_KEYWORD IDENTIFIER IN_KEYWORD ExpressionWithoutAssignment Block

UnsafeStatement ::= UNSAFE_KEYWORD Block
InlineCppStatement ::= CPP_KEYWORD Block

Block ::= NL CURLY_OPEN NLSEMI <<withOff StatementList? "NO_ASSIGN">> NLSEMI CURLY_CLOSE {
    extends=Statement
    mixin="org.serenityos.jakt.psi.misc.JaktBlockMixin"
}

/*************************************
 *               TYPES               *
 *************************************/

private BaseType ::=
      DictionaryType
    | ArrayType
    | SetType
    | TupleType
    | PlainType

GenericSpecialization ::= LESS_THAN <<commaOrEolList Type>> GREATER_THAN {pin=1 extends=Type}
ArrayType ::= BRACKET_OPEN Type BRACKET_CLOSE {extends=Type}
DictionaryType ::= BRACKET_OPEN Type COLON Type BRACKET_CLOSE {pin=3 extends=Type}
SetType ::= CURLY_OPEN Type CURLY_CLOSE {pin=1 extends=Type}
TupleType ::= PAREN_OPEN <<commaList Type>> PAREN_CLOSE {pin=1 extends=Type}
PlainType ::= NamespaceQualifier* IDENTIFIER GenericSpecialization? {
    extends=Type
    implements="org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    mixin="org.serenityos.jakt.psi.reference.JaktPlainTypeMixin"
}

Type ::=
      RawType
    | WeakType
    | OptionalType
    | BaseType
{
    implements="org.serenityos.jakt.psi.api.JaktTypeable"
    mixin="org.serenityos.jakt.psi.misc.JaktTypeMixin"
}

RawType ::= RAW_KEYWORD BaseType {pin=1 extends=Type}
WeakType ::= WEAK_KEYWORD BaseType QUESTION_MARK {pin=1 extends=Type}
OptionalType ::= BaseType QUESTION_MARK {extends=Type}

TypeAnnotation ::= COLON Type {extends=Type}
GenericBounds ::= LESS_THAN <<commaOrEolList GenericBound>> GREATER_THAN
GenericBound ::= IDENTIFIER {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktGenericBoundMixin"
}

/*************************************
 *            EXPRESSIONS            *
 *************************************/

// TODO: Pretty much every expression can start with an arbitrary amount of newlines
Expression ::=
      IndexedAccessExpression // Must come before AccessExpression
    | AccessExpression // Must come before CallExpression
    | CallExpression // Must come before RelationalBinaryExpression (because of generics)
    | AssignmentBinaryExpression // Must come before other binary expressions
    | LogicalOrBinaryExpression
    | LogicalAndBinaryExpression
    | BitwiseOrBinaryExpression
    | BitwiseXorBinaryExpression
    | BitwiseAndBinaryExpression
    | RelationalBinaryExpression
    | ShiftBinaryExpression
    | AddBinaryExpression
    | MultiplyBinaryExpression
    | PrefixUnaryExpression
    | PostfixUnaryExpression
    | ParenExpression
    | FieldAccessExpression // Must come before ThisExpression
    | ThisExpression
    | RangeExpression
    | ArrayExpression
    | DictionaryExpression
    | SetExpression
    | TupleExpression
    | MatchExpression
    | Literal
    | PlainQualifier

private ExpressionWithoutAssignment ::= <<withOn Expression "NO_ASSIGN">> {name="expression"}

CallExpression ::= Expression GenericSpecialization? ArgumentList
ArgumentList ::= PAREN_OPEN NL  [<<commaOrEolList Argument>>] PAREN_CLOSE
Argument ::= LabeledArgument | UnlabeledArgument
LabeledArgument ::= IDENTIFIER COLON ExpressionWithoutAssignment {
    implements="org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    mixin="org.serenityos.jakt.psi.reference.JaktLabeledArgumentMixin"
}
UnlabeledArgument ::= ExpressionWithoutAssignment

AccessExpression ::= Expression DOT Access
Access ::= FieldLookup | TupleLookup {
    implements=[
        "org.serenityos.jakt.psi.api.JaktTypeable"
        "org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    ]
    mixin="org.serenityos.jakt.psi.reference.JaktAccessMixin"
}
private FieldLookup ::= IDENTIFIER
private TupleLookup ::= DECIMAL_LITERAL

IndexedAccessExpression ::= Expression BRACKET_OPEN Expression BRACKET_CLOSE
FieldAccessExpression ::= THIS_KEYWORD? DOT IDENTIFIER {
    implements="org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    mixin="org.serenityos.jakt.psi.reference.JaktFieldAccessExpressionMixin"
}
ThisExpression ::= THIS_KEYWORD {
    mixin="org.serenityos.jakt.psi.reference.JaktThisExpressionMixin"
}

ParenExpression ::= PAREN_OPEN ExpressionWithoutAssignment PAREN_CLOSE
RangeExpression ::= Expression DOT_DOT Expression

ArrayExpression ::= BRACKET_OPEN NL [SizedArrayBody | ElementsArrayBody] NL BRACKET_CLOSE
SizedArrayBody ::= ExpressionWithoutAssignment SEMICOLON NumericLiteral
ElementsArrayBody ::= <<commaOrEolList ExpressionWithoutAssignment>>

DictionaryExpression ::= BRACKET_OPEN NL [<<commaOrEolList DictionaryElement>> | COLON] NL BRACKET_CLOSE
DictionaryElement ::= Expression COLON NL Expression
SetExpression ::= CURLY_OPEN NL <<commaOrEolList ExpressionWithoutAssignment>>? NL CURLY_CLOSE
TupleExpression ::= PAREN_OPEN NL <<commaOrEolList ExpressionWithoutAssignment>> NL PAREN_CLOSE

MatchExpression ::= MATCH_KEYWORD ExpressionWithoutAssignment NL MatchBody
MatchBody ::= CURLY_OPEN NL MatchCases? NL CURLY_CLOSE
private MatchCases ::= <<commaOrEolList MatchCase>>
MatchCase ::= MatchCaseHead MatchCaseTrail {
    implements="org.serenityos.jakt.psi.api.JaktPsiScope"
    mixin="org.serenityos.jakt.psi.declaration.JaktMatchCaseMixin"
}
MatchCaseTrail ::= NL FAT_ARROW (Block | Expression)
MatchCaseHead ::= <<pipeList (ELSE_KEYWORD | MatchPattern | ExpressionWithoutAssignment)>>

MatchPattern ::= PlainQualifier [PAREN_OPEN <<commaOrEolList DestructuringPart>>? PAREN_CLOSE]
DestructuringPart ::= [DestructuringLabel COLON] DestructuringBinding
DestructuringLabel ::= IDENTIFIER {
    implements="org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    mixin="org.serenityos.jakt.psi.reference.JaktDestructuringLabelMixin"
}
DestructuringBinding ::= IDENTIFIER {
    implements="org.serenityos.jakt.psi.declaration.JaktDeclaration"
    mixin="org.serenityos.jakt.psi.declaration.JaktDestructuringBindingMixin"
}

NamespaceQualifier ::= IDENTIFIER COLON_COLON {
    implements=[
        "org.serenityos.jakt.psi.api.JaktTypeable"
        "org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    ]
    mixin="org.serenityos.jakt.psi.reference.JaktNamespaceQualifierMixin"
}
PlainQualifier ::= NamespaceQualifier* IDENTIFIER {
    extends=Expression
    implements=[
        "org.serenityos.jakt.psi.api.JaktTypeable"
        "org.serenityos.jakt.psi.named.JaktNameIdentifierOwner"
    ]
    mixin="org.serenityos.jakt.psi.reference.JaktPlainQualifierMixin"
}

/*************************************
 *        BINARY EXPRESSIONS         *
 *************************************/

fake BinaryExpression ::= Expression+ {
    methods=[left="Expression[0]" right="Expression[1]"]
}

LogicalOrBinaryExpression ::= Expression LogicalOrOp NL Expression
LogicalAndBinaryExpression ::= Expression KEYWORD_AND NL Expression
// &ExpressionLead helps disambiguate rules in PatternHead production
BitwiseOrBinaryExpression ::= Expression PIPE NL &ExpressionLead Expression
BitwiseXorBinaryExpression ::= Expression CARET NL Expression
BitwiseAndBinaryExpression ::= Expression AMPERSAND NL Expression
RelationalBinaryExpression ::= Expression RelationalOp NL Expression
ShiftBinaryExpression ::= Expression ShiftOp NL Expression
AddBinaryExpression ::= Expression AddOp NL Expression
MultiplyBinaryExpression ::= Expression MultiplyOp NL Expression
// TODO: Restrict LHS?
// TODO: Invert Expression{WithoutAssignment} => Expression{WithAssignment}, as this can't be a part
//       of the expression tree root
AssignmentBinaryExpression ::= Expression AssignmentOp NL Expression {rightAssociative=true}
fake UnaryExpression ::= PrefixOp? NL Expression PostfixOp?
PrefixUnaryExpression ::= PrefixOp NL Expression {elementType=UnaryExpression}
PostfixUnaryExpression ::= Expression PostfixOp {elementType=UnaryExpression}

private AddOp ::= PLUS | MINUS
private MultiplyOp ::= ASTERISK | SLASH | PERCENT
private ShiftOp ::= ARITH_LEFT_SHIFT | LEFT_SHIFT | ARITH_RIGHT_SHIFT | RIGHT_SHIFT
private RelationalOp ::= LESS_THAN_EQUALS | LESS_THAN | GREATER_THAN_EQUALS | GREATER_THAN | DOUBLE_EQUALS | NOT_EQUALS
private LogicalOrOp ::= KEYWORD_OR | DOUBLE_QUESTION_MARK
private AssignmentOp ::=
      PLUS_EQUALS
    | MINUS_EQUALS
    | ASTERISK_EQUALS
    | SLASH_EQUALS
    | PERCENT_EQUALS
    | ARITH_LEFT_SHIFT_EQUALS
    | LEFT_SHIFT_EQUALS
    | ARITH_RIGHT_SHIFT_EQUALS
    | RIGHT_SHIFT_EQUALS
    | EQUALS
private PrefixOp ::=
      PLUS_PLUS
    | MINUS_MINUS
    | MINUS
    | KEYWORD_NOT
    | TILDE
    | AMPERSAND RAW_KEYWORD
    | ASTERISK
private PostfixOp ::=
      PLUS_PLUS
    | MINUS_MINUS
    | EXCLAMATION_POINT
    | KEYWORD_AS (QUESTION_MARK | EXCLAMATION_POINT) Type
    | KEYWORD_IS Type

/*************************************
 *             LITERALS              *
 *************************************/

Literal ::=
      STRING_LITERAL
    | BYTE_CHAR_LITERAL
    | CHAR_LITERAL
    | NumericLiteral
    | BooleanLiteral
    {extends=Expression}

NumericLiteral ::= (BINARY_LITERAL | OCTAL_LITERAL | HEX_LITERAL | DECIMAL_LITERAL) NumericSuffix?
    {extends=Expression}

NumericSuffix ::= 'uz' | 'f32' | 'f64' | 'u8' | 'u16' | 'u32' | 'u64' | 'i8' | 'i16' | 'i32' | 'i64'

BooleanLiteral ::= TRUE_KEYWORD | FALSE_KEYWORD {extends=Expression}

/*************************************
 *             HELPERS               *
 *************************************/

private NL ::= NEWLINE*
private NLSEMI ::= (NEWLINE | SEMICOLON)*
private meta commaList ::= <<p>> (COMMA <<p>>)* COMMA?
private meta pipeList ::= <<p>> (NL PIPE NL <<p>>)*
private meta commaOrEolList ::= <<p>> (MemberSeparator <<p>>)* MemberSeparator?
